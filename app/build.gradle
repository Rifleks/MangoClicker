plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'rifleks.clicker'
    compileSdk 36

    defaultConfig {
        applicationId "rifleks.clicker"
        minSdk 24
        targetSdk 36
        versionCode 5
        versionName "1.2"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "APP_NAME", "\"MangoClicker\""
        buildConfigField "boolean", "IS_DEBUG", "false"
    }

    signingConfigs {
        release {
            storeFile file(findProperty('storeFile') ?: 'debug.keystore')
            storePassword findProperty('storePassword') ?: ''
            keyAlias findProperty('keyAlias') ?: ''
            keyPassword findProperty('keyPassword') ?: ''
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFileName = "MangoClicker_${variant.versionName}(${variant.versionCode}).apk"
            output.outputFileName = outputFileName
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += [
                '-Xjvm-default=all',
                '-opt-in=kotlin.RequiresOptIn'
        ]
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    packagingOptions {
        resources {
            excludes += [
                    'META-INF/AL2.0',
                    'META-INF/LGPL2.1',
                    'META-INF/*.kotlin_module'
            ]
        }
    }
}

dependencies {
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.activity:activity-ktx:1.10.1'
    implementation 'androidx.fragment:fragment-ktx:1.8.8'

    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.2'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.9.2'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}